plugins {
    id 'java-library'
    id 'groovy'
    id 'jacoco'
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
}

apply plugin: 'idea'

repositories {
    mavenCentral()
    jcenter()
}

group = 'io.github.Chase22.telegram'
version = '1.0.0-SNAPSHOT'

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

idea {
    module {
        sourceDirs -= project.sourceSets.integrationTest.groovy.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.groovy.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'io.undertow:undertow-core:2.0.27.Final'
    implementation 'org.slf4j:slf4j-api:1.7.28'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'

    // Use the latest Groovy version for Spock testing
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.7'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.objenesis:objenesis:3.0.1'
    testImplementation 'cglib:cglib-nodep:3.2.9'
    testImplementation 'junit:junit:4.12'

    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.12.1'

    integrationTestImplementation 'org.codehaus.groovy:groovy-all:2.5.7'
    integrationTestImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    integrationTestImplementation 'junit:junit:4.12'

    integrationTestImplementation 'org.telegram:telegrambots:4.4.0.1'

    integrationTestImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.12.1'
    integrationTestImplementation 'org.gebish:geb-waiting:2.2'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
    }
}

test {
    finalizedBy jacocoTestReport
}